resource "azurerm_virtual_network" "main" {
  name                = "vnet-${var.name_prefix}"
  resource_group_name = var.resource_group_name
  location            = var.location
  address_space       = var.address_space
  tags                = var.tags
}

resource "azurerm_subnet" "aks" {
  name                 = "snet-aks"
  resource_group_name  = var.resource_group_name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = var.aks_subnet_prefixes
}

resource "azurerm_subnet" "api_server" {
  count                = length(var.api_server_subnet_prefixes) > 0 ? 1 : 0
  name                 = "snet-aks-apiserver"
  resource_group_name  = var.resource_group_name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = var.api_server_subnet_prefixes

  delegation {
    name = "aks-apiserver-delegation"

    service_delegation {
      name = "Microsoft.ContainerService/managedClusters"
    }
  }
}

resource "azurerm_subnet" "private_endpoints" {
  count                = length(var.private_endpoint_subnet_prefixes) > 0 ? 1 : 0
  name                 = "snet-private-endpoints"
  resource_group_name  = var.resource_group_name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = var.private_endpoint_subnet_prefixes
}

resource "azurerm_subnet" "management" {
  count                = length(var.management_subnet_prefixes) > 0 ? 1 : 0
  name                 = "snet-management"
  resource_group_name  = var.resource_group_name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = var.management_subnet_prefixes
}
